apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// This handles both kotlin and java. Use kapt instead of annotationProcessor
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.gatheringhallstudios.mhworlddatabase"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
}

androidExtensions {
    // Enables KTX viewbinding to work on viewholders as well
    experimental = true
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    /*
     * Google Support Libraries
     */
    def google_version = "28.0.0-beta01"
    implementation "com.android.support:appcompat-v7:${google_version}"
    implementation "com.android.support:cardview-v7:${google_version}"
    implementation "com.android.support:gridlayout-v7:${google_version}"
    implementation "com.android.support:recyclerview-v7:${google_version}"
    implementation "com.android.support:design:${google_version}"
    implementation "com.android.support:interpolator:${google_version}"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"

    /*
     * Leak Canary
     */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'


    /*
     * Android Kotlin Extensions
     * TODO: I can't get this to build for some reason. :/
     */
    //implementation "androidx.core:core-ktx:1.0.0-alpha1"

    /*
     * Android Architecture Components
     */
    def architecture_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:${architecture_version}"
    kapt "android.arch.lifecycle:compiler:${architecture_version}"
    implementation "android.arch.persistence.room:runtime:${architecture_version}"
    kapt "android.arch.persistence.room:compiler:${architecture_version}"
    testImplementation "android.arch.core:core-testing:${architecture_version}"
    testImplementation "android.arch.persistence.room:testing:${architecture_version}"

    /*
     * Android Navigation Component
     */
    def nav_version = "1.0.0-alpha02"
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    // optional - Test helpers
    androidTestImplementation "android.arch.navigation:navigation-testing:$nav_version"
    androidTestImplementation "android.arch.navigation:navigation-testing-ktx:$nav_version"

    /*
     * Butterknife
     */
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'

    /*
     * Adapter Delegates
     * https://github.com/sockeqwe/AdapterDelegates
     */
    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    /*
     * Groupie
     * https://github.com/lisawray/groupie
     */
    implementation 'com.xwray:groupie:2.1.0'
    implementation 'com.xwray:groupie:2.1.0'
    implementation 'com.xwray:groupie-kotlin-android-extensions:2.1.0'

    /*
     * VectorMaster
     * https://github.com/harjot-oberai/VectorMaster
     */
    implementation 'com.sdsmdg.harjot:vectormaster:1.1.3'

    /*
     * Testing
     */
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}

